# Maintainer: Roberto Catini <roberto.catini@gmail.com> 

pkgname=divvyd
pkgrel=1
pkgver=0
pkgdesc="Ripple peer-to-peer network daemon"
arch=('i686' 'x86_64')
url="https://github.com/ripple/divvyd"
license=('custom:ISC')
depends=('protobuf' 'openssl' 'boost-libs')
makedepends=('git' 'scons' 'boost')
checkdepends=('nodejs')
backup=("etc/$pkgname/divvyd.cfg")
source=("git://github.com/ripple/divvyd.git#branch=master")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	scons build/divvyd
}

check() {
	cd "$srcdir/$pkgname"
	npm install
	npm test
	build/divvyd --unittest
}

package() {
	cd "$srcdir/$pkgname"
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D build/divvyd "$pkgdir/usr/bin/divvyd"
	install -D -m644 doc/divvyd-example.cfg "$pkgdir/etc/$pkgname/divvyd.cfg"
	mkdir -p "$pkgdir/var/lib/$pkgname/db"
	mkdir -p "$pkgdir/var/log/$pkgname"
}
